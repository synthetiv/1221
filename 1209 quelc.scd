(

// headphone warning: malcontent RLPFs
// seems like there's lots of potential here to feed the chaotic triggers generated by the system back into it in some other form (triggering Latch, etc)... but simplicity may be nicer

{

	arg f1 = 162.1, f2 = 81, f3 = 79.9, cut = 1600, rq = 0.3, del = 0.12, fb = 3100, amp = 0.7;
	var o1 = SinOsc.ar(f1);
	var o2 = SinOsc.ar(f2);
	var o3 = SinOsc.ar(f3);
	var min = o1.abs.min(o2.abs).min(o3.abs) * o1.sign * o2.sign * o3.sign * 1.2.pow(LFTri.kr(0.43, 1)) * Line.kr(dur: 7);
	var modcut = cut * 1.3.pow(SinOsc.ar(0.6, [0, 1.5pi])) + DelayL.ar(LocalIn.ar(2).reverse, 1, del, fb);
	var filter = RLPF.ar(min.tanh, modcut.min(SampleRate.ir / 2), rq);
	var out = (filter * amp).tanh;
	LocalOut.ar(filter);
	(out * -4.dbamp).scope;

}.play;

)